 
var USE_BC = true // Set this to false if you don't have bc library.
var one = (USE_BC)? require("bc").number(1) : 1

var IN = 1
var OUT = 2

function shrink(t, start, end) {
    var out = [start]
    var insert = global.table.insert
    for each (var k = 2 to $t - 1) {
        insert(out, t[k])
    }
    insert(out, end)
    return out
}

function increment(t, key, inc) {
    var ks = table.concat(key, ',')
    if (t[ks] == null) {
        t[ks] = [inc, key]
    }
    else {
        t[ks][0] = t[ks][0] + inc
    }
}

function euler_393(n) {
    var noarrow = [one, []]
    for (var k = 0; k < n; k += 1) {
        table.insert(noarrow[1], 0)
    }
    var ns = table.concat(noarrow[1], ',')
    var D = {ns: noarrow}
    var D1
    var t1

    // Columns
    for (var i = 0; i < n; i += 1) {
        // Rows
        for (var j = 0; j < n; j += 1) {
            D1 = {}
            for each (var t,v in pairs(D)) {
                if (v[1][0] == 0) {
                    if ((j < n - 1) && (v[1][1] != IN)) {
                        t1 = shrink(v[1], v[1][1] + IN, OUT)
                        increment(D1, t1, v[0])
                    }
                    if ((j < n - 1) && (v[1][1] != OUT)) {
                        t1 = shrink(v[1], v[1][1] + OUT, IN)
                        increment(D1, t1, v[0])
                    }
                }
                else if (v[1][0] == IN) {
                    if ((j < n - 1) && (v[1][1] != IN)) {
                        t1 = shrink(v[1], v[1][1] + IN, 0)
                        increment(D1, t1, v[0])
                    }
                    t1 = shrink(v[1], v[1][1], IN)
                    increment(D1, t1, v[0])
                }
                else if (v[1][0] == OUT) {
                    if ((j < n - 1) && (v[1][1] != OUT)) {
                        t1 = shrink(v[1], v[1][1] + OUT, 0)
                        increment(D1, t1, v[0])
                    }
                    t1 = shrink(v[1], v[1][1], OUT)
                    increment(D1, t1, v[0])
                }
                else { // v[1][0] == IN + OUT
                    t1 = shrink(v[1], v[1][1], 0)
                    increment(D1, t1, v[0])
                }
            }
            D = D1
        }
    }
    return (D[ns] != null)? D[ns][0] : 0
}
print(euler_393(10))
